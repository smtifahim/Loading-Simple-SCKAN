# INSERT pattern for Simple SCKAN's simplified partonomy using ilxtr:isPartOf relation - August 2024
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX partOf: <http://purl.obolibrary.org/obo/BFO_0000050>
PREFIX ILX: <http://uri.interlex.org/base/ilx_>
PREFIX ilxtr: <http://uri.interlex.org/tgbugs/uris/readable/>
PREFIX UBERON: <http://purl.obolibrary.org/obo/UBERON_>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>

# SELECT DISTINCT ?sub_region ?sub_region_label ?super_region ?super_region_label

INSERT { ?sub_region ilxtr:isPartOf ?super_region }

WHERE
{
    ## To test with select statement uncomment the next two lines.
    # FILTER (?super_region_label = 'heart')
    # ?sub_region rdfs:label ?sub_region_label. ?super_region rdfs:label ?super_region_label. 

    
    ## To include both UBERON partOf and ILX 'is Part Of' (ILX:0112785) relation
    VALUES ?part_of {ILX:0112785 partOf:}
    
    ?sub_region rdfs:subClassOf* [rdf:type owl:Restriction ;
                                           owl:onProperty ?part_of ; 
                                           owl:someValuesFrom ?super_region].
           
    FILTER (CONTAINS (str(?sub_region), 'UBERON_') || CONTAINS (str(?sub_region), 'ilx_'))
    FILTER (CONTAINS (str(?super_region), 'UBERON_') || CONTAINS (str(?super_region), 'ilx_'))
}
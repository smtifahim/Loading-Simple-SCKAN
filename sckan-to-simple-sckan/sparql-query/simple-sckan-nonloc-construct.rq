PREFIX BFO: <http://purl.obolibrary.org/obo/BFO_>
PREFIX ilxtr: <http://uri.interlex.org/tgbugs/uris/readable/>
PREFIX partOf: <http://purl.obolibrary.org/obo/BFO_0000050>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

# CONSTRUCT {
#   ?neuron_type ?npo_simple_prop ?object .
#   ?object rdfs:label ?object_l .
# }

Select ?neuron_type ?npo_simple_prop ?object_label

WHERE {
  FILTER (regex(str(?neuron_type), "sdcol"))

  VALUES (?npo_prop ?npo_simple_prop) {
    # other phenotype mappings (non-locational)
    (ilxtr:hasForwardConnectionPhenotype ilxtr:hasForwardConnection)
    (ilxtr:hasFunctionalCircuitRolePhenotype ilxtr:hasFunctionalCircuitRole)
    (ilxtr:hasAnatomicalSystemPhenotype ilxtr:hasNeuronalPhenotype)
    (ilxtr:hasProjectionPhenotype ilxtr:hasProjection)
    (ilxtr:hasCircuitRolePhenotype ilxtr:hasCircuitRole)
    (ilxtr:hasMolecularPhenotype ilxtr:hasMolecule)
    (ilxtr:hasInstanceInTaxon ilxtr:isObservedInSpecies)
    (ilxtr:hasBiologicalSex ilxtr:hasPhenotypicSex)
  }

  {
    # --- Other phenotypes in intersection form ---
    ?neuron_type a owl:Class ;
      (owl:equivalentClass | rdfs:subClassOf) [
        a owl:Class ;
        owl:intersectionOf ?bn0
      ] .
    ?bn0 rdf:rest*/rdf:first [
      a owl:Restriction ;
      owl:onProperty ?npo_prop ;
      owl:someValuesFrom ?object
    ] .
    OPTIONAL { ?object rdfs:label ?object_l }
  }

  UNION

  {
    # --- Other phenotypes as superclass axioms ---
    ?neuron_type a owl:Class ;
      rdfs:subClassOf [
        a owl:Restriction ;
        owl:onProperty ?npo_prop ;
        owl:someValuesFrom ?object
      ] .
    OPTIONAL { ?object rdfs:label ?object_l }
  }

  ?object rdfs:label ?object_label
  FILTER(!isBlank(?object))
}
ORDER BY ?neuron_type